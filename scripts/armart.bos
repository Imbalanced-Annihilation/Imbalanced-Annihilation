// Arm Light Lightning Tank

piece flare,flarept,turret,sleeve,base,barrel;

static-var restore_delay,fire;

#define SIG_AIM 2
#define SMOKEPIECE1 base
#include "smokeunit.h"
#include "exptype.h"
#include "hitweap.h"

Create()
	{
	hide flare;
	hide flarept;
	fire = 0;
	restore_delay=3000;
	start-script SmokeUnit();
	}

SetMaxReloadTime(time)
	{
	restore_delay = time * 2;
	}

RestoreAfterDelay()
	{
	sleep restore_delay;
	turn turret to y-axis 0 speed <90>;
	turn sleeve to x-axis 0 speed <50>;
	}

AimPrimary(heading,pitch)
	{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <90>;
	turn sleeve to x-axis (0-pitch) speed <50>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	start-script RestoreAfterDelay();
	return(TRUE);
	}

FirePrimary()
		{
		show flare;
		move barrel to z-axis [-1.4] speed [300];
		sleep 150;
		hide flare;
		wait-for-move barrel along z-axis;
		move barrel to z-axis [0] speed [3.0];	
		}

AimFromPrimary(piecenum)
	{
	piecenum=turret;
	}

QueryPrimary(piecenum)
	{
	piecenum=flarept;
	}

SweetSpot(piecenum)
	{
	piecenum=base;
	}

#include "EXPtype.h"

Killed( severity, corpsetype )
	{
	hide flare;
	hide flarept;
	if (severity <= 25)
		{
		corpsetype = 1;

		// Blow off the pieces in varied ways
		explode barrel type	BITMAPONLY | BITMAP1;
		explode base type	BITMAPONLY | BITMAP2;
		explode	flare type	BITMAPONLY | BITMAP3;
		explode sleeve type	BITMAPONLY | BITMAP4;
		explode turret type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	if (severity <= 50)
		{
		corpsetype = 2;

		// Blow off the pieces in varied ways
		explode barrel type	FALL | BITMAP1;
		explode base type	BITMAPONLY | BITMAP2;
		explode	flare type	FALL | BITMAP3;
		explode sleeve type	SHATTER | BITMAP4;
		explode turret type	FALL | BITMAP3;
		return( 0 );
		}

	if (severity <= 99)
		{
		corpsetype = 3;

		// Blow off the pieces in varied ways
		explode barrel type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode base type	BITMAPONLY | BITMAP2;
		explode	flare type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		explode sleeve type	SHATTER | BITMAP4;
		explode turret type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		return( 0 );
		}

	// If severity is over 50, the unit just disintigrated.
	corpsetype = 3;

	explode barrel type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode base type	BITMAPONLY | BITMAP2;
	explode flare type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode sleeve type	SHATTER | EXPLODE_ON_HIT | BITMAP4;
	explode turret type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	return( 0 );
	}
